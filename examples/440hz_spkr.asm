; этот пример должен воспроизвести на встроенном динамике
; звук частотой 440 Гц (нота ля первой октавы)
; длительностью 3 сек.

	org	0000
	lxi	b,2640	; число повторений
	mvi	d,01		; бит спикера установлен

loop:	; начало цикла
	mov	a,d
	out	0xf4		; устанавливаем бит спикера (адрес порта спикера настраивается в меню Настройка->модули внешних устройств)
	xri	1		; инвертируем состояние бита спикера
	mov	d,a		; запоминаем состояние бита

	; команды в начале и конце цикла занимают 
	; 5 + 10 + 7 + 5 +
	; 5 + 5 + 4 + 10 = 51 такт, значит остается еще
	; 2272 - 51 = 2221 такта 

	; осуществляем задержку
	mvi	h,82		; 7 тактов + 27*82 = 2221
loop1:	nop
	nop
	nop
	dcr	h
	jnz	loop1		; каждая итерация цикла 4*3 + 5 + 10 = 27 тактов

	; конец задержки

	; уменьшаем счетчик цикла
	dcx	b
	; сравниваем с нулем (команда уменьшения регистровой пары не действует на флаги
	; поэтому приходиться проверять регистры b и с по отдельности)
	mov	a,b
	ora	c
	jnz	loop
	nop
	hlt

; период звука 1/440 = 2,272 мс или
; 2000000 / 440 = 4545 тактов процессора (при его тактовой частоте 2Мг)
; полупериод звука равен 4545/2 = 2272 такта.
; за 3 секунды пройдет 3*440 = 1320 периодов (6000000 тактов).
; или 1320*2 = 2640 полупериодов
	